/**
 * =====================================
 * jsonAv( value )
 *
 * RETURNS:
 *	prepares a string for inclusion as an element in a JSON array
 *
 * PARAMETERS:
 *	value = the value of the Array element
 *
 * DEPENDENCIES:
 *	BaseElements Plugin version 3.0 or greater
 *
 * HISTORY:
 *	MODIFIED on 2015-JAN-24 Daniel Smith, dansmith65@gmail.com
 *		allow the value to be json with leading/trailing whitespace
 *		return "?" on error and store error message in $json.error
 *	MODIFIED on 2015-JAN-24 Todd Geist, todd@geistinteractive.com
 *		changed the name to jsonAv( arrayValues )
 *	MODIFIED on 2015-JAN-07 Todd Geist, todd@geistinteractive.com
 *		changed name to jsonNewArrayValue ( value )
 *	CREATED on 2015-JAN-06 Todd Geist, todd@geistinteractive.com
 *
 * =====================================
 */

Let ( [
	json = BE_JSONPath ( value ; "$" ) ;  // attempt to read value as json
	firstAndLast = Left ( json ; 1 ) & Right ( json ; 1 ) ;
	valueIsJson = firstAndLast = "{}" or firstAndLast = "[]" ;
	encoded = If ( valueIsJson ;
		json ;
		BE_JSON_Encode ( "" ; value )
	) ;
	isError = not IsEmpty ( BE_JSON_Error_Description ) ;
	result =
		Case (
			isError ;
			"?" ;
			
			valueIsJson ;
			encoded & "," ;
			
			/* else */
			Right (  // extract the encoded value
				encoded ;
				Length ( encoded ) - Position ( encoded ; ":" ; 1 ; 1 )
			) & ","
		)
	;
	$json.error = If ( isError ;
		If ( BE_Version = "?" ;
			"BaseElements plugin is not available" ;
			Substitute ( BE_JSON_Error_Description ; Char ( 10 ) ; "Â¶" )
		) ;
		/* else: don't modify the error */
		$json.error
	)
] ;
	result
)